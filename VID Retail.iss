; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
[Files]
;Source: C:\Program Files (x86)\SAP\SAP Business One\AddOnInstallAPI.dll; Flags: dontcopy
Source: AddOnInstallAPI.dll; Flags: dontcopy
;Source: C:\Program Files (x86)\Codejock Software\ISSkin\ISSkin.dll; DestDir: {app}; Flags: dontcopy
;Source: C:\Program Files (x86)\Codejock Software\ISSkin\Styles\Office2007.cjstyles; DestDir: {tmp}; Flags: dontcopy
Source: bin\Debug\Interop.SAPbobsCOM.dll; DestDir: {app}
Source: bin\Debug\Interop.SAPbouiCOM.dll; DestDir: {app}
Source: bin\Debug\VID_Retail_Secure\VID_Retail.exe; DestDir: {app}
Source: bin\Debug\VID_Retail_Secure\VID_Retail.pdb; DestDir: {app}
Source: bin\Debug\Docs\EDVDCROYAL.xls; DestDir: {app}\Docs\
Source: bin\Debug\Docs\EDVDRET.xls; DestDir: {app}\Docs\
Source: bin\Debug\Docs\UDFRECTDA.xls; DestDir: {app}\Docs\
Source: bin\Debug\Menus\Menu.xml; DestDir: {app}\Menus\
Source: bin\Debug\Forms\AjusteStockLF.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\CambioMasivoEstadoOT.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Categoria.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Clusters.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\ControlTrasladosBodegas.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Departamento.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\DespachoATiendas.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Familia.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\FiltroAceptacionRecep.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Grupo.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Parametros.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Password.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Periodos.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Precios.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\RecepcionenTienda.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\RecepcionenTiendaAprobacion.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Surtido.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\Tiendas.srf; DestDir: {app}\Forms\
Source: bin\Debug\Forms\TransferenciaOrdenServicio.srf; DestDir: {app}\Forms\
Source: bin\Debug\VisualD.ADOSBOScriptExecute.dll; DestDir: {app}
Source: bin\Debug\VisualD.ChooseFromListSubQuery.dll; DestDir: {app}
Source: bin\Debug\VisualD.Core.dll; DestDir: {app}
Source: bin\Debug\VisualD.Dimensions.dll; DestDir: {app}
Source: bin\Debug\VisualD.GlobalVid.dll; DestDir: {app}
Source: bin\Debug\VisualD.Main.dll; DestDir: {app}
Source: bin\Debug\VisualD.MainObjBase.dll; DestDir: {app}
Source: bin\Debug\VisualD.MasterDataMatrixForm.dll; DestDir: {app}
Source: bin\Debug\VisualD.Matrix_Helper.dll; DestDir: {app}
Source: bin\Debug\VisualD.MenuConfFr.dll; DestDir: {app}
Source: bin\Debug\VisualD.MultiFunctions.dll; DestDir: {app}
Source: bin\Debug\Visuald.ReportWindowFr.dll; DestDir: {app}
Source: bin\Debug\VisualD.SBOCrystalPreview.dll; DestDir: {app}
Source: bin\Debug\VisualD.SBOFunctions.dll; DestDir: {app}
Source: bin\Debug\VisualD.SBOGeneralService.dll; DestDir: {app}
Source: bin\Debug\VisualD.SBOObjectMg1.dll; DestDir: {app}
Source: bin\Debug\VisualD.uEncrypt.dll; DestDir: {app}
Source: bin\Debug\VisualD.untLog.dll; DestDir: {app}
Source: bin\Debug\VisualD.vkBaseForm.dll; DestDir: {app}
Source: bin\Debug\VisualD.vkFormInterface.dll; DestDir: {app}
Source: bin\Debug\VisuaD.Core.exe; DestDir: {app}; Flags: nocompression
[Setup]
UsePreviousLanguage=no
AppName=AFOne
AppPublisher=Visual Developer
AppVerName=VID Retail - VisualD
AppPublisherURL=http://www.visuald.cl
AppSupportURL=http://www.visuald.cl
AppUpdatesURL=http://www.visuald.cl
DefaultDirName={code:GetDefaultAddOnDir}
;OutputBaseFileName={code:GetSetupName}
OutputBaseFileName=Setup
DisableDirPage=true
Compression=lzma
SolidCompression=true
UsePreviousAppDir=false
AppendDefaultDirName=true
PrivilegesRequired=admin
WindowVisible=false
WizardSmallImageFile=compiler:WizModernSmallImage-IS.bmp
//WizardImageFile=C:\Program Files (x86)\Codejock Software\ISSkin\Styles\Office2007Gray.bmp
AppContact=soporte@visuald.cl
//SetupIconFile=C:\Software\-iconos\Settings and System Utilities\YaST.ico
[Messages]
BeveledLabel=Visual Developer - www.visuald.cl
[Registry]
Root: HKLM; Subkey: SOFTWARE\VID\Addons\{code:GetAddOnName}; ValueType: string; ValueName: InstallDir; ValueData: {code:GetDefaultAddOnDir}; Flags: uninsdeletevalue
[Languages]
Name: spanish; MessagesFile: compiler:Languages\Spanish.isl
[UninstallDelete]
Type: files; Name: {app}\{code:GetSetupName}.EXE
[Run]
Filename: {app}\VisuaD.Core.exe
[Code]
type
   TSHFileOpStruct = record
     Wnd    				: HWND;
     wFunc  				: UINT;
     pFrom  				: PChar;
     pTo    				: PChar;
     fFlags  				: Word;
     fAnyOperationsAborted 	: BOOL;
     hNameMappings			: HWND;
     lpszProgressTitle: PChar;
   end;

const
   { $EXTERNALSYM FO_COPY }
   FO_COPY           = $0002;
   { $EXTERNALSYM FOF_SILENT }
   FOF_SILENT                 = $0004;
   { $EXTERNALSYM FOF_NOCONFIRMATION }
   FOF_NOCONFIRMATION         = $0010;

var
CurrentLocation : string;
AddOnDir        : string;
FinishedInstall : Boolean;
Params          : string;
i               : integer;

//Copy
function SHFileOperation(const lpFileOp: TSHFileOpStruct):Integer; external 'SHFileOperation@shell32.dll stdcall';

//Skin support
//procedure LoadSkin(lpszPath: String; lpszIniFileName: String); external 'LoadSkin@files:isskin.dll stdcall';
//procedure UnloadSkin(); external 'UnloadSkin@files:isskin.dll stdcall';//
//function  ShowWindow(hWnd: Integer; uType: Integer): Integer; external 'ShowWindow@user32.dll stdcall';

//SAP B1
function EndInstallEx(Dir:String;Ok:Boolean): integer; external 'EndInstallEx@files:AddOnInstallAPI.dll stdcall';
function EndUninstall(path: string; succeed: boolean): integer; external 'EndUninstall@files:AddOnInstallAPI.dll stdcall';
function SetAddOnFolder(srcPath : string): integer; external 'SetAddOnFolder@files:AddOnInstallAPI.dll stdcall';
function RestartNeeded :integer; external 'RestartNeeded@files:AddOnInstallAPI.dll stdcall delayload ';

function GetAddOnName(dummy: String): string;
begin
   result := 'AFOne';
end;

function GetSetupName(dummy: String): string;
begin
   result := 'Setup';
end;

function PreparePaths() : Boolean;
var
position : integer;
aux : string;
begin
   if pos('|', paramstr(2)) <> 0 then //la ruta puede venir en le parametro 2 o 4
   begin
      aux := paramstr(2);
      position := Pos('|', aux);
      AddOnDir := Copy(aux,1, position - 1);
      Result := True;
   end
   else
   if pos('|', paramstr(4)) <> 0 then //la ruta puede venir en le parametro 2 o 4
   begin
      aux := paramstr(4);
      position := Pos('|', aux);
      AddOnDir := Copy(aux,1, position - 1);
      Result := True;
   end
   else
   begin
   MsgBox('El Instalador debe ser ejecutado desde SAP Business One.', mbInformation, MB_OK);
   EndInstallEx('',false);//Avisa a Sap B1 que se aborto la instalacion
   Result := False;
   end;
end;

function GetDefaultAddOnDir(Param : string): string;
begin
   result := AddOnDir;
end;

Function ExistStrInParam(StrInParam:string) : boolean;
var
 j: integer;
begin
result:=false;
  for j:=0 to ParamCount do
  if ParamStr(j)=StrInParam then
  begin
   result:=true;
   break;
  end;
end;

function InitializeSetup(): Boolean;
var
ResultCode      : Integer;
UninstallerPath : String;
begin
   //ExtractTemporaryFile('Office2007.cjstyles');
   //LoadSkin(ExpandConstant('{tmp}\Office2007.cjstyles'), 'NormalBlack.ini');

   Params:='';
   for i:=0 to ParamCount do
   Params:=Params+' Param'+inttoStr(i)+' = '+paramstr(i)+#13;
   //MsgBox(Params, mbInformation, MB_OK);

   if ExistStrInParam('/U') then
   begin
    if RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SOFTWARE\VID\Addons\'+GetAddOnName(''),'InstallDir', CurrentLocation) then
    UninstallerPath:=CurrentLocation + '\unins000.exe';
    //MsgBox('UninstallerPath '+UninstallerPath, mbInformation, MB_OK);
    Exec(UninstallerPath, '/SILENT', '', SW_SHOW, ewWaitUntilTerminated, ResultCode);
    EndUninstall('',true);
    result:=False;
   end
   else
   begin
   result := PreparePaths;
   end;
end;

procedure DeinitializeSetup();
begin
	//ShowWindow(StrToInt(ExpandConstant('{wizardhwnd}')), 0);
	//UnloadSkin();
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin
   Result := True;
   case CurPageID of
       wpSelectDir :
       begin
           AddOnDir := ExpandConstant('{app}');
       end;
       wpFinished :
       begin
          if FinishedInstall then
          begin
             SetAddOnFolder(AddOnDir);
             EndInstallEx('',True);
          end;
       end;
   end;
end;

Function ExtractFileNameParam(StrParam:string) : string;
var
 j: integer;
begin
 result:=StrParam;
 j:=1;
 repeat
 result:=Copy(StrParam,j,Length(StrParam));
 j:=j+1;
 until FileExists(result) or (j>=Length(StrParam));
end;

procedure CopyFile(FromFileName, ToFileName: string);
var
  ShellInfo: TSHFileOpStruct;
  Files    : String;
begin
  Files:=FromFileName+#0+#0;
  ShellInfo.wFunc:=FO_COPY;
  ShellInfo.pFrom:=PChar(Files);
  ShellInfo.pTo:=PChar(ToFileName);
  ShellInfo.fFlags:=FOF_NOCONFIRMATION or FOF_SILENT;
  SHFileOperation(shellinfo);
end;

procedure CurStepChanged(CurStep: TSetupStep);
Var
SetupFile : string;
begin
   if CurStep = ssPostInstall then
   begin
    SetupFile:=ExtractFileNameParam(ParamStr(1));
    //FileCopy(SetupFile,AddOnDir+'\'+GetSetupName('')+'.exe',False); esta opcion no funciona en este caso
    //MsgBox('Origen '+SetupFile+' Destino '+AddOnDir+'\'+GetSetupName('')+'.exe', mbInformation, MB_OK);
    DeleteFile(AddOnDir+'\'+GetSetupName('')+'.exe');
    CopyFile(SetupFile,AddOnDir+'\'+GetSetupName('')+'.exe');
    FinishedInstall := True;
   end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
{ no se usa por el momento debido a que el instalador llama al unist000.exe
	case CurUninstallStep of
		ssDone : EndUninstall('',true);
	end;
}
end;
[Dirs]
Name: {app}\Docs
Name: {app}\Menus
Name: {app}\Reports
Name: {app}\Forms
